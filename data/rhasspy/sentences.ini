[TestIntent]
this is a test 

[ChangeLightState]
light_name = (wohnzimmerlampe | garagenlicht) {name}
light_state = (ein | an | aus) {state}
schalte (die | das) <light_name> <light_state>
licht <light_state>

[ChangeBrightness]
(stelle | setze) [die] helligkeit auf (1..100){percent!int} [prozent]

[ChangeTemp]
(stelle | setze) [die] temperatur auf (kalt | warm){temp}

[GetTime]
wie spät ist es
sag mir die uhrzeit
uhrzeit
wie viel uhr ist es

[GetWeatherInfo]
(wie | sag mir) [ist] das wetter in $city{city}

[GetWeeklyWeatherInfo]
(was | wie | sag mir) [ist] das wetter für [die] (kommende | nächste) woche in $city{city}

[GetSunriseInfo]
(wann | um wie viel uhr) geht [die] sonne in $city{city} auf

[GetSunsetInfo]
(wann | um wie viel uhr) geht [die] sonne in $city{city} unter

[CoronaDistrictInfo]
(wie sehen | sag mir) [die] corona zahlen für [den] landkreis ($districts){district} [aus]

[CoronaStateInfo]
(wie sehen | sag mir) [die] corona zahlen für [das] bundesland ($states){state} [aus]

[WikipediaInfo]
erzähl mir etwas über ($topics){topic}

[StartRadio]
mach das radio an
radio an

[StopRadio]
mach das radio aus
radio aus

[NextRadio]
nächster sender
nächste station

[PrevRadio]
vorherige station
zurück

[setAlarm]
wecke mich um (0..23){alarm_hour!int} uhr [(1..59){alarm_minute!int}]
stell einen wecker für (0..23){alarm_hour!int} uhr [(1..59){alarm_minute!int}]

[stopAlarm]
stop
ruhe
hör auf
sei leise

[deleteAlarm]
lösche [den] (0..23){alarm_hour!int} uhr [(1..59){alarm_minute!int}] (alarm | wecker)

[getRemainingTime]
wie lange läuft der timer [noch]

[setTimer]
([(1..24){timer_hour!int} (stunde|stunden)] [(1..59){timer_minute!int} (minuten|minute)] [(1..59){timer_second!int} (sekunden|sekunde)] timer)
stelle einen [(1..24){timer_hour!int} (stunde|stunden)] [(1..59){timer_minute!int} (minuten|minute)] [(1..59){timer_second!int} (sekunden|sekunde)] timer

[GoodMorning]
guten morgen 
ich bin wach
